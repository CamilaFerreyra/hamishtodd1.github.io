<script id="simulationVertexShader" type="x-shader/x-vertex"> 
	attribute vec3 aPos2;
	attribute vec2 aTexCoord;
	varying   vec2 tc;
void main(void)
{
	gl_Position = vec4(aPos2, 1.);
	tc = aTexCoord;
}
</script> 

<script id="simulationFragmentShader" type="x-shader/x-fragment"> 
precision highp float;
	uniform sampler2D samp2;
	uniform float dt;
	uniform float dth2;
	uniform float a;
	uniform float b;
	uniform float eps;
	varying vec2 tc;
	const float n = 128., ny = 64., dx = 1./n, dy = 1./ny, dz = dy/n;
void main(void)
{
	vec4 t = texture2D(samp2, tc);
	float u = t.r,  v = t.g;
	float du = dt*(u*(1.0 - u)*(u - a) - v);
	float vnew = v + dt*eps*(b*u - v);
	float dym = tc.y - dy;  // clamp y
	if(dym < 0.) dym = tc.y;
	float dyp = tc.y + dy;
	if(dyp > 1.) dyp = tc.y;

	float z = mod(tc.y, dy);  // clamp z
	float dzm = z - dz;
	if(dzm < 0.) dzm = tc.y;
	else dzm = tc.y - dz;
	float dzp = z + dz;
	if(dzp > dy) dzp = tc.y;
	else dzp = tc.y + dz;
	du += (
			texture2D(samp2, vec2(tc.x, dyp) ).r +
			texture2D(samp2, vec2(tc.x, dym) ).r +
			texture2D(samp2, vec2(tc.x, dzp) ).r +
			texture2D(samp2, vec2(tc.x, dzm) ).r +
			texture2D(samp2, vec2(tc.x + dx, tc.y) ).r +
			texture2D(samp2, vec2(tc.x - dx, tc.y) ).r +
			- 6.*u)*dth2;
	gl_FragColor = vec4(u + du, vnew, du, 0. );
}